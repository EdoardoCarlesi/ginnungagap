#########################################################################
# This is a sample GNUplot script that will read the files generated by #
# ginnungagap (provided the grafic writer is used) and plot the first   #
# plane in the files as an image to a postscript file.                  #
#                                                                       #
# You will most likely want to adapt a few values and also the names of #
# the files that should be plotted and the resulting figure names.      #
#                                                                       #
# This script requires a fairly recent GNUPlot (>= 4.5).                #
#########################################################################

set terminal postscript enhanced eps color size 17cm,17cm

# The resolution of the file
res = 200
# The minimum and maximum values at which to box the color range for the
# density (dens), the whitenoise (wn)m and the velocities (vel)
densmin = -.3
densmax = .3
wnmin = -5.
wnmax = 5.
velmin = -150.
velmax = 150.

set size sq
set format x ""
set format y ""
set xrange [-0.5:res-.5]
set yrange [-0.5:res-.5]

# DENSITY PLOT
set cbrange [densmin:densmax]
set palette defined (densmin "#FF0000", (densmax + densmin)*.5 "#FFFFFF", densmax "#0000FF")
unset key
set output '00200_delta2.eps'
# Older versions will require  record=200x200 array=200x200  here (or
# whatever your resolution is).
plot \
	'ic_delta' \
	binary record=(res,res) array=(res,res) skip=56 scan=xy \
	w image

# WHITE NOISE PLOT
set cbrange [wnmin:wnmax]
set palette defined (wnmin "#FF0000", (wnmax + wnmin)*.5 "#FFFFFF", wnmax "#0000FF")
unset key
set output '00200_wn2.eps'
plot \
	'../scale/wn_delta' \
	binary record=(res,res) array=(res,res) skip=56 scan=xy \
	w image

# VELOCITY PLOTS
set cbrange [velmin:velmax]
set palette defined (velmin "#FF0000", (velmax + velmin)*.5 "#FFFFFF", velmax "#0000FF")
set output '00200_velx2.eps'
plot \
	'ic_velx' \
	binary record=(res,res) array=(res,res) skip=56 scan=xy \
	w image

set output '00200_vely2.eps'
plot \
	'ic_vely' \
	binary record=(res,res) array=(res,res) skip=56 scan=xy \
	w image

set output '00200_velz2.eps'
plot \
	'ic_velz' \
	binary record=(res,res) array=(res,res) skip=56 scan=xy \
	w image

